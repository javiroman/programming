import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.1'
}

group 'hello-drools'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.drools', name: 'drools-compiler', version: '7.19.0.Final'
    compile group: 'org.kie.server', name: 'kie-server-client', version: '7.19.0.Final'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// By default, shadowJar.manifest inherits from jar.manifest.
jar {
    manifest {
        attributes('Main-Class': 'HelloDrools')
    }
}

// https://docs.jboss.org/drools/release/7.14.0.Final/drools-docs/html_single/#_building_and_running_drools_in_a_fat_jar
// https://stackoverflow.com/questions/46690139/exception-child-services-have-no-parent-when-starting-optaplanner-application

// Shadow Pluging Manual: https://libraries.io/github/johnrengelman/shadow
// imperceptiblethoughts.com/shadow/configuration/merging/#merging-service-descriptor-files
shadowJar {
    transform(AppendingTransformer) {
        resource = 'META-INF/kie.conf'
    }
}

// FIXED with:
// ./gradlew shadowJar
// jar xf hello-drools-1.0-SNAPSHOT-all.jar META-INF/kie.conf
// remove lines in META-INF/kie.conf
//  org.kie.api.internal.assembler.KieAssemblers = +org.optaplanner.core.impl.solver.kie.KieSolverAssemblerService
//  org.kie.api.internal.assembler.KieAssemblers = +org.kie.pmml.assembler.PMMLAssemblerService
// reasemble with:
// jar uf hello-drools-1.0-SNAPSHOT-all.jar META-INF/kie.conf